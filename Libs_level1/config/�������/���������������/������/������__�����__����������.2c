//#define _БайтКод
//Визуальный объект - список элементов таблицы БД
//******************************************************************************************
//Атрибуты объекта доступные через точку (переменные с признаком Экспорт)
//******************************************************************************************
Перем ВидЭлемента;
Перем ФайлНастройки;

//******************************************************************************************
//Внутренние переменные
//******************************************************************************************
Перем Таблица;
Перем КолСтрокОбновления;
Перем КоличествоКолонок;
Перем БуферЭлементов[];


Перем НомерНовойСтроки;
Перем НомерРедактируемойСтроки;

Перем БылаИнициализация;

Перем ЗаданыИдентификаторы;
Перем СписокИмен[];

Перем НомерКартинкиЭлемента;
Перем НомерКартинкиЭлементаПомеч;
Перем ВиртуальныйРежим;
Перем ОбъектПрототип;
Перем ТипВид;


Перем КонтекстФормы;

//Предопредленные атрибуты
Перем ТекущийЭлемент;


//Задание типов переменных для IntelliSense
//Таблица:Grid


//Перем СписокМеню;//СписокМеню:СписокЗначений
//___________________________________________________________________________________________
//Динамическое получение атрибутов
Функция ПолучитьАтрибут(Имя) Экспорт
	
	НомСтр=Таблица.СтрокаОтображения();
	Если НомСтр>0 Тогда//режим отображения формул таблицы
		Таблица.ПолучитьСтрокуПоНомеру(НомСтр);
	Иначе
		НомСтр=Таблица.ТекущаяСтрока();
		ПроверитьБуферДанных(НомСтр);
	КонецЕсли
	
	//Предопредленные атрибуты
	Если ВРЕГ(Имя)="ТЕКУЩИЙДОКУМЕНТ" Тогда
		Возврат ТекущийЭлемент;
	ИначеЕсли ВРЕГ(Имя)="ТЕКУЩИЙЭЛЕМЕНТ" Тогда
		Возврат ТекущийЭлемент;
	Иначе
		Попытка
			Возврат Таблица.ПолучитьАтрибут(Имя);
		Исключение
			Ошибка(ОписаниеОшибки());
		КонецПопытки
	КонецЕсли
КонецФункции

//___________________________________________________________________________________________
//Строковое представление объекта
Функция ПолучитьПредставлениеОбъекта()
	Возврат "<>";
КонецФункции

//___________________________________________________________________________________________
//Конструктор (вызывается при инициализации объекта)
Процедура Конструктор(ВидОбъекта,Диалог)Экспорт
	БылаИнициализация=0;
	НомерНовойСтроки=0;
	НомерРедактируемойСтроки=0;
	
	Если ТипЗначения(Диалог)=0 Тогда
		Ошибка("Объект доступен только в визуальном режиме");
	Иначе
		Таблица=Диалог;
	КонецЕсли;
	
	СписокМеню=СоздатьОбъект("СписокЗначений");
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
//Предопределенная процедура - вызывается при первом открытии формы
Процедура Инициализация(УстТипВид,Объект=0,РежимОткрытияФормы="")Экспорт
	Перем ТекЭлемент;
	
	ТипВид=УстТипВид;
	//ОбъектПрототип.Инициализация(ТипВид);
	ОбъектПрототип=СоздатьОбъект(ТипВид); 
	
	//Сообщить(ТипЗначенияСтр(КонтекстФормы));
//	ТекЭлемент=КонтекстФормы.Форма.ТекущийЭлемент();
	Если ТипЗначения(Объект)>3 Тогда
		ТекЭлемент=Объект.ТекущийЭлемент();
	КонецЕсли
	Если ТипЗначенияСтр(РежимОткрытияФормы)="Число" Тогда
	    КонтекстФормы.Форма.РежимВыбора(РежимОткрытияФормы);
	КонецЕсли;     
	Для А=1 По ОбъектПрототип.ПолучитьКоличествоАтрибутов() Цикл
		ИмяАтрибута=ОбъектПрототип.ПолучитьИмяАтрибута(А);
		СписокИмен[ИмяАтрибута]=1;
	КонецЦикла
	
	КоличествоКолонок=Таблица.КоличествоКолонок();
	ЗаданыИдентификаторы=0;
	Для А=0 По КоличествоКолонок-1 Цикл
		Если Сокрлп(Таблица.ИдентификаторКолонки(А))<>"" Тогда
			ЗаданыИдентификаторы=1;
			Прервать;
		КонецЕсли
	КонецЦикла
	
	Если ЗаданыИдентификаторы=0 Тогда//не заданы колонки в визуальной структуре таблицы
		КоличествоАтрибутов=ОбъектПрототип.ПолучитьКоличествоАтрибутов();
		Таблица.КоличествоФиксКолонок(1);
		Таблица.КоличествоКолонок(1+КоличествоАтрибутов);
		Таблица.КоличествоФиксСтрок(1);
		
		КоличествоКолонок=КоличествоАтрибутов;
		Для А=1 По КоличествоАтрибутов Цикл
			ИмяАтрибута=ОбъектПрототип.ПолучитьИмяАтрибута(А);
			Таблица.ИдентификаторКолонки(А,ИмяАтрибута);
			Таблица.ЗаголовокКолонки(А,ИмяАтрибута)
			Таблица.ТипКолонки(ИмяАтрибута,ОбъектПрототип.ПолучитьТипПоля(ИмяАтрибута));
		КонецЦикла;
	Иначе
		Таблица.КоличествоКолонок(КоличествоКолонок+1);
		//Таблица.ШиринаКолонки(КоличествоКолонок,0);//невидимая колонка - для возможности визуальной сортировки колонок
		Для А=0 По КоличествоКолонок-1 Цикл
			ИмяКолонки=Таблица.ИдентификаторКолонки(А);
			Если СписокИмен[ИмяКолонки]=1 Тогда//есть такой атрибут у объекта в БД
				Таблица.ТипКолонки(ИмяКолонки,ОбъектПрототип.ПолучитьТипПоля(ИмяКолонки));
			КонецЕсли
		КонецЦикла;
	КонецЕсли
	Таблица.КоличествоКолонок(КоличествоКолонок+1);
	//Таблица.ШиринаКолонки(КоличествоКолонок,0);//невидимая колонка - для возможности визуальной сортировки колонок
	Таблица.ВидимостьКолонки(КоличествоКолонок,0);//невидимая колонка - для возможности визуальной сортировки колонок
	
	КолСтрокОбновления=10;
	КоличествоСтрок=ОбъектПрототип.КоличествоЭлементов();
	
	Таблица.КоличествоСтрок(1+КоличествоСтрок);
	Таблица.УстановитьШиринуКолонки(0,25);
	
	ВиртуальныйРежим=Таблица.ВиртуальныйРежим();
	ЗапуститьТаймер();
	Обновить();
	Таблица.ТекущаяЯчейка(1,1);
	Если ТипЗначения(ТекЭлемент)<>0 Тогда//ищем текущий элемент
		Попытка
			ТекИД=ТекЭлемент.ПолучитьИдентификаторОбъекта();
		Исключение
			ТекИД=0;
		КонецПопытки
		Если ПустоеЗначение(ТекИД)=0 Тогда
			Для А=1 По КоличествоСтрок Цикл
				Если ВиртуальныйРежим=1 Тогда
					ОбработатьЯчейку(А,0,0,0,0,0,0);
				КонецЕсли
				Если ПолучитьЭлементСтроки(А).ПолучитьИдентификаторОбъекта()=ТекИД Тогда
					Таблица.ТекущаяЯчейка(А,1);
					Прервать;
				КонецЕсли
			КонецЦикла
		КонецЕсли
	КонецЕсли
	УстановитьТаймер("УстановитьФокус",10);
	БылаИнициализация=1;
	Таблица.Обновить();
КонецПроцедуры

//___________________________________________________________________________________________
Процедура ВыполнитьКомандуМеню(ИмяКоманды,ТекОбъект)Экспорт//выполнение команды контекстного меню
	Попытка
		Если ИмяКоманды="Изменить" Тогда
			Редактирование();
		ИначеЕсли ИмяКоманды="Удалить" ИЛИ ИмяКоманды="Снять удаление" Тогда
			Удалить();
		ИначеЕсли ИмяКоманды="Обновить" Тогда
			Обновить();
		Иначе
			Сообщить("ТаблицаБДСписок. Неизвестная команда меню: "+ИмяКоманды);
		КонецЕсли
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки
КонецПроцедуры//ВыполнитьКоманду

//___________________________________________________________________________________________
Процедура ПоКнопкеМеню()
	ТекОбъект=ПолучитьЭлементСтроки(Таблица.ТекущаяСтрока());
	//ТекОбъект:ТаблицаБД
	
	Если ПустоеЗначение(ТекОбъект)=1 Тогда
		Возврат;
	КонецЕсли
	
	
	//создаем контекстное меню
	СписокМеню=СоздатьОбъект("СписокЗначений");
	СписокМеню.ДобавитьЗначение(Контекст,"Изменить");
	Если ТекОбъект.ПометкаУдаления() Тогда
		СписокМеню.ДобавитьЗначение(Контекст,"Снять удаление");
	Иначе
		СписокМеню.ДобавитьЗначение(Контекст,"Удалить");
	КонецЕсли
	СписокМеню.ДобавитьЗначение(Контекст,"Обновить");
	//СписокМеню.ДобавитьЗначение("","");
	ВызватьСобытие("ТаблицаБДСписок:СозданиеКонтекстногоМеню",ТекОбъект,СписокМеню);
	
	Перем Ном;
	Перем Значение;
	Перем Представление;
	Рез=СписокМеню.ВыбратьЗначение(Значение,,Ном,,1,Представление);
	Если Рез=1 Тогда
		Значение.ВыполнитьКомандуМеню(Представление,ТекОбъект);
		Пауза(5);
		Таблица.ПерерисоватьСтроку(Таблица.ТекущаяСтрока());
	КонецЕсли
КонецПроцедуры//ПоКнопкеМеню


Процедура УстановитьФокус()
	Таблица.Фокус(1);
	УдалитьТаймер("УстановитьФокус");
КонецПроцедуры

//___________________________________________________________________________________________
Процедура ЗапуститьТаймер()
	УдалитьТаймер("ОбработкаТаймера");
	УдалитьТаймер("ОбработкаТаймераВиртРеж");
	
	Если КонтекстФормы.Форма.РежимВыбора() Тогда
		Таблица.Редактирование(0);//для быстрого поиска - если не нужно то закомментируйте эту строку...
		Возврат;//таймер не запускаем и обновление формы в режиме выбора не делаем
	КонецЕсли
	
	Если ВиртуальныйРежим=0 Тогда
		УстановитьТаймер("ОбработкаТаймера",ПериодОбновленияБД*1000);
	Иначе
		Таблица.Редактирование(0);
		УстановитьТаймер("ОбработкаТаймераВиртРеж",ПериодОбновленияБД*1000);
	КонецЕсли
КонецПроцедуры


//___________________________________________________________________________________________
Процедура ПроверитьБуферДанных(НомСтр)
	Если НомСтр>0 Тогда
		ТекущийЭлемент=БуферЭлементов[НомСтр];
		Если Таблица.ВиртуальныйРежим() Тогда
			Если ТипЗначения(БуферЭлементов[НомСтр])=0 Тогда
				Ном=0;
				ОбъектПрототип.ВыбратьЭлементыБД(НомСтр-1,КолСтрокОбновления);
				Пока ОбъектПрототип.ПолучитьЭлемент()=1 Цикл
					БуферЭлементов[НомСтр+Ном]=ОбъектПрототип.ТекущийЭлемент();
					Ном=Ном+1;
				КонецЦикла;
			КонецЕсли
		КонецЕсли
	КонецЕсли
КонецПроцедуры//ПроверитьБуферДанных

//___________________________________________________________________________________________
//Обрабочик событий визуального объекта
//Возвращает:
//0 - событие не обработано (передается в форму для дальнейшей обработки)
//1 - событие обработано (далее в форму не передается)
Функция ОбработкаСобытия(ИмяСобытия,НомСтр,НомКол,СтрТекст,НомКартинки,НомФормат,НомЦветТекста,НомЦветФона) Экспорт//ИмяСобытия - имя события как оно задано в окне свойств конфигуратора
	Перем Ном;
	Если БылаИнициализация=0 Тогда
		Возврат 0;
	КонецЕсли
	
	Если ИмяСобытия="Виртуальный режим" Тогда
		ОбработатьЯчейку(НомСтр,НомКол,СтрТекст,НомКартинки,НомФормат,НомЦветТекста,НомЦветФона);
		Возврат 1;
		
	ИначеЕсли ИмяСобытия="Отображение" Тогда//событие - признак начала отрисовки таблицы (НомСтр - номер отрисовываемой строки)
		ПроверитьБуферДанных(НомСтр);
		Возврат 0;
		
		
		//*************************************************
		//режим прямого отображения и редактирования данных
		
	ИначеЕсли ИмяСобытия="Нажатие клавиши" Тогда
		Если СостояниеКлавиши("CONTROL") Тогда
			Возврат 0;
		КонецЕсли
		
		НомерРедактируемойСтроки=0;
		Если НажатаяКлавиша()=27 Тогда//ESCAPE
			Возврат ОтменитьВводНовойСтроки();
		ИначеЕсли НажатаяКлавиша()=45 Тогда//INSERT
			НоваяСтрока();
			Возврат 1;
		ИначеЕсли НажатаяКлавиша()=46 Тогда//DEL
			Удалить();
			Возврат 1;
		ИначеЕсли НажатаяКлавиша()=116 Тогда//F5
			Обновить();
		ИначеЕсли НажатаяКлавиша()=120 Тогда//F9
			Копировать();
		ИначеЕсли НажатаяКлавиша()=113 Тогда//F2
			НачалоРедактирования();
			Возврат 0;
		ИначеЕсли НажатаяКлавиша()=13 Тогда//ENTER
			Если Выбрать()=1 Тогда
				Возврат 1;
			Иначе
				НачалоРедактирования();
				Возврат 0;
			КонецЕсли
		КонецЕсли
	ИначеЕсли ИмяСобытия="Начало выделения" Тогда
		//		ТекЭлемент=БуферЭлементов[НомСтр];
		//		ТекущийЭлемент=ТекЭлемент;
		Возврат 0;
	ИначеЕсли ИмяСобытия="Двойн.лев.кнопка" Тогда
		Если Таблица.ТекущаяСтрока()>0 Тогда
			Если Выбрать()=1 Тогда
				Возврат 1;
			КонецЕсли
		КонецЕсли
	КонецЕсли
	
	Если ИмяСобытия="Начало редактирования" Тогда
		Если Выбрать()=1 Тогда
			Возврат 1;
		КонецЕсли
		НачалоРедактирования();
	ИначеЕсли ИмяСобытия="Окончание редактирования" Тогда
		Если СостояниеКлавиши("ESCAPE") Тогда
			ОтменитьВводНовойСтроки();
			Возврат 0;
		ИначеЕсли НомерНовойСтроки Тогда
			Если СостояниеКлавиши("TAB") Тогда
				Возврат 0;
			ИначеЕсли НЕ СостояниеКлавиши("RETURN") Тогда
				НомерНовойСтроки=0;
			ИначеЕсли КоличествоКолонок-1<>Таблица.ТекущаяКолонка() Тогда
				Таблица.ТекущаяКолонка(Таблица.ТекущаяКолонка()+1);//смещение вправо
				АктивизироватьПоле();
				Возврат 0;//не записываем - продолжаем редактирование
			КонецЕсли
		КонецЕсли
		
		Записать();
		//Обновить();
	ИначеЕсли ИмяСобытия="Правая кнопка" Тогда
		ПоКнопкеМеню();
	КонецЕсли;
	Возврат 0;
КонецФункции

Процедура УстановитьДанные(НомСтр)
	Для А=0 По КоличествоКолонок-1 Цикл
		ИмяКолонки=Таблица.ИдентификаторКолонки(А);
		Если СписокИмен[ИмяКолонки]=1 Тогда//есть такой атрибут у объекта в БД
			ОбъектПрототип.УстановитьАтрибут(ИмяКолонки,Таблица.Значение(НомСтр,А));
		КонецЕсли
	КонецЦикла
КонецПроцедуры


//******************************************************************************************
//Методы объекта доступные через точку(процедуры и функции с признаком Экспорт)
//******************************************************************************************
Функция ТекущийЭлемент();
	Возврат ПолучитьЭлементСтроки(Таблица.ТекущаяСтрока());
КонецФункции

//___________________________________________________________________________________________
Функция ПолучитьЭлементСтроки(Ном)
	Если ВиртуальныйРежим Тогда
		ПроверитьБуферДанных(Ном);
		Возврат БуферЭлементов[Ном];
	Иначе
		Возврат Таблица.Значение(Ном,КоличествоКолонок);
	КонецЕсли
КонецФункции

//___________________________________________________________________________________________
Процедура УстановитьЭлементСтроки(Ном,Элемент)
	Если ВиртуальныйРежим Тогда
		БуферЭлементов[Ном]=Элемент;
	Иначе
		Таблица.Значение(Ном,КоличествоКолонок,Элемент);
	КонецЕсли
КонецПроцедуры



///////////////////////////////////////////////////////////////////////
//Процедура обработки виртуального режима таблицы
Процедура ОбработатьЯчейку(НомСтр,НомКол,СтрТекст,НомКартинки,НомФормат,НомЦветТекста,НомЦветФона)
	Перем Ном;
	Если НомСтр=0 Тогда//заголовки таблицы
		Если НомКол>0 Тогда
			СтрТекст=Таблица.ЗаголовокКолонки(НомКол);
		КонецЕсли
		Возврат;
	КонецЕсли
	
	ТекЭлемент=БуферЭлементов[НомСтр];
	ТекущийЭлемент=ТекЭлемент;
	Если ТипЗначения(ТекЭлемент)>=10 Тогда
		Если НомКол=0 Тогда
			НомКартинки=ТекЭлемент.ПолучитьКартинкуЭлемента();
		Иначе
			ИмяКолонки=Таблица.ИдентификаторКолонки(НомКол);
			Если СписокИмен[ИмяКолонки]=1 Тогда//есть такой атрибут у объекта в БД
				СтрТекст=ТекЭлемент.ПолучитьАтрибут(ИмяКолонки);
				Возврат;
			КонецЕсли
		КонецЕсли
	КонецЕсли
	СтрТекст="";
КонецПроцедуры



Процедура ОбработкаТаймера()
	Если НомерРедактируемойСтроки Тогда
		Возврат;
	КонецЕсли
	Если НомерНовойСтроки Тогда
		Возврат;
	КонецЕсли
	
	Таблица.КоличествоСтрок(1+ОбъектПрототип.КоличествоЭлементов());
	ЗаполнитьТаблицу();
КонецПроцедуры

Процедура ОбработкаТаймераВиртРеж()
	Если НомерРедактируемойСтроки Тогда
		Возврат;
	КонецЕсли
	Если НомерНовойСтроки Тогда
		Возврат;
	КонецЕсли
	
	Таблица.КоличествоСтрок(1+ОбъектПрототип.КоличествоЭлементов());
	КорректировкаЧислаСтрок();
	
	//очищаем буфер Элементов
	БуферЭлементов=СоздатьОбъект("Массив");
	Таблица.ПерерисоватьОкно();
КонецПроцедуры


Процедура ЗаполнитьТаблицу()
	Ном=0;
	КолСтрок=ОбъектПрототип.КоличествоЭлементов();
	Если КолСтрок=0 Тогда
		КолСтрок=1;//резервирование места для новой записи
	КонецЕсли
	Таблица.КоличествоСтрок(1+КолСтрок);
	КорректировкаЧислаСтрок();
	Ном=Ном+1;
	ОбъектПрототип.ВыбратьЭлементыБД();
	Пока ОбъектПрототип.ПолучитьЭлемент()=1 Цикл
		УстановитьЭлементСтроки(Ном,ОбъектПрототип.ТекущийЭлемент());
		
		Таблица.Картинка(Ном,0,ОбъектПрототип.ПолучитьКартинкуЭлемента());
		Для А=0 По КоличествоКолонок-1 Цикл
			ИмяКолонки=Таблица.ИдентификаторКолонки(А);
			Если СписокИмен[ИмяКолонки]=1 Тогда//есть такой атрибут у объекта в БД
				Значение=ОбъектПрототип.ПолучитьАтрибут(ИмяКолонки);
			Иначе
				Значение="";
			КонецЕсли
			Таблица.Значение(Ном,А,Значение);
		КонецЦикла;
		Ном=Ном+1;
	КонецЦикла;
КонецПроцедуры

Процедура ВиртуальныйРежим(Реж)Экспорт
	ВиртуальныйРежим=Реж;
	Таблица.ВиртуальныйРежим(Реж);
	
	ЗапуститьТаймер();
КонецПроцедуры

//___________________________________________________________________________________________
//резервирование места для новой записи
Процедура КорректировкаЧислаСтрок()
	Если Таблица.КоличествоСтрок()=1 Тогда
		Таблица.КоличествоСтрок(2);
	КонецЕсли
	Если Таблица.ТекущаяСтрока()<0 Тогда
		//Таблица.ТекущаяЯчейка(Таблица.КоличествоСтрок()-1,1);
	КонецЕсли
КонецПроцедуры

//___________________________________________________________________________________________
Функция ОтменитьВводНовойСтроки()
	Если НомерНовойСтроки Тогда
		Таблица.КоличествоСтрок(НомерНовойСтроки);
		КорректировкаЧислаСтрок();
		Таблица.ТекущаяЯчейка(НомерНовойСтроки-1,1);
		НомерРедактируемойСтроки=0;
		НомерНовойСтроки=0;
		Возврат 1;
	КонецЕсли
	НомерРедактируемойСтроки=0;
	НомерНовойСтроки=0;
	Возврат 0;
КонецФункции

//___________________________________________________________________________________________
Процедура Удалить()Экспорт
	Если Таблица.ТекущаяСтрока()>0 И Таблица.КоличествоСтрок()>1 Тогда
		Если НомерНовойСтроки=0 Тогда
			ТекЭлемент=ТекущийЭлемент();
			Если ТипЗначения(ТекЭлемент)<>0 Тогда
				Если ТекЭлемент.ПометкаУдаления()=0 Тогда
					ТекЭлемент.УстановитьИдентификаторОбъекта(ТекЭлемент.ПолучитьИдентификаторОбъекта());
					Попытка
						ТекЭлемент.Удалить(0);
					Исключение
						Сообщить(ОписаниеОшибки());
					КонецПопытки
				Иначе
					ТекЭлемент.УстановитьИдентификаторОбъекта(ТекЭлемент.ПолучитьИдентификаторОбъекта());
					ТекЭлемент.СнятьПометкуУдаления();
				КонецЕсли
			КонецЕсли 
		КонецЕсли
		
		//		//автоматически смещаемся вниз для удобства при массовой пометке/разметке ълементов
		//		Перем статПредНомерСтроки Статично;//переменная для автовыбра направления
		//		
		//		Номер=Таблица.ТекущаяСтрока();
		//		Если Номер>Число(статПредНомерСтроки) Тогда//направление вниз
		//			Если Номер<Таблица.КоличествоСтрок()-1 Тогда
		//				Таблица.ТекущаяЯчейка(Номер+1,1);
		//			КонецЕсли
		//		ИначеЕсли Номер<Число(статПредНомерСтроки) Тогда//направление вверх
		//			Если Номер>1 Тогда
		//				Таблица.ТекущаяЯчейка(Номер-1,1);
		//			КонецЕсли
		//		КонецЕсли
		//		статПредНомерСтроки=Номер;
	КонецЕсли
	НомерРедактируемойСтроки=0;
	НомерНовойСтроки=0;
	Обновить();
КонецПроцедуры

//___________________________________________________________________________________________
Процедура Обновить()
	Записать();
	НомерРедактируемойСтроки=0;
	НомерНовойСтроки=0;
	
	Если ВиртуальныйРежим=0 Тогда
		ОбработкаТаймера();
	Иначе
		ОбработкаТаймераВиртРеж(); 
	КонецЕсли
КонецПроцедуры

//___________________________________________________________________________________________
Процедура Записать()
	Если НомерРедактируемойСтроки Тогда
		ТекЭлемент=ТекущийЭлемент();
		Если ТипЗначения(ТекЭлемент)<>0 Тогда
			ОбъектПрототип.УстановитьИдентификаторОбъекта(ТекЭлемент.ПолучитьИдентификаторОбъекта());
		Иначе
			ОбъектПрототип.Новый();
		КонецЕсли
		Если НомерНовойСтроки Тогда
			УстановитьДанные(НомерНовойСтроки);
		Иначе
			УстановитьДанные(НомерРедактируемойСтроки);
		КонецЕсли
		
		ОбъектПрототип.Записать();
		УстановитьЭлементСтроки(Таблица.ТекущаяСтрока(),ОбъектПрототип.ТекущийЭлемент());
		//Таблица.Фокус(1);
	КонецЕсли
	НомерРедактируемойСтроки=0;
	НомерНовойСтроки=0;
КонецПроцедуры

//___________________________________________________________________________________________
Процедура НоваяСтрока(Активировать=1,ЭтоГруппа=0)Экспорт
	Если ВиртуальныйРежим Тогда
		ОткрытьФорму(ТипВид,,,ЭтоГруппа);
		Возврат;
	КонецЕсли
	
	Таблица.Редактирование(1);
	Если ТипЗначения(Активировать)=0 Тогда//вдруг это вызов метода с пропущенным параметром
		Активировать=1;
	КонецЕсли
	
	НомерРедактируемойСтроки=Таблица.КоличествоСтрок();
	НомерНовойСтроки=Таблица.КоличествоСтрок();
	Таблица.КоличествоСтрок(НомерНовойСтроки+1);
	Таблица.ТекущаяЯчейка(НомерНовойСтроки,1);
	Если ЭтоГруппа=1 Тогда
		Попытка
			ОбъектПрототип.НоваяГруппа();
		Исключение
		КонецПопытки
	КонецЕсли
	
	Таблица.Картинка(НомерНовойСтроки,0,ОбъектПрототип.ПолучитьКартинкуЭлемента());
	Если Активировать=1 Тогда
		АктивизироватьПоле();
	КонецЕсли
КонецПроцедуры

Процедура НоваяГруппа()Экспорт
	НоваяСтрока(1,1);
КонецПроцедуры

//___________________________________________________________________________________________
Процедура Копировать()Экспорт
	ПредСтрока=Таблица.ТекущаяСтрока();
	Если ПредСтрока<1 Тогда
		Возврат;
	КонецЕсли
	
	Если ВиртуальныйРежим Тогда
		Объект=СоздатьОбъект(ТипВид);
		Источник=ПолучитьЭлементСтроки(ПредСтрока);
		Если ПустоеЗначение(Источник)=0 Тогда
			Объект.НайтиЭлемент(Источник);
			Объект.НоваяКопия();
		КонецЕсли
		ОткрытьФорму(Объект);
		Возврат;
	КонецЕсли
	
	НоваяСтрока(0);
	
	ТекСтрока=Таблица.ТекущаяСтрока();
	Для А=0 По КоличествоКолонок-1 Цикл
		Таблица.Значение(ТекСтрока,А,Таблица.Значение(ПредСтрока,А));
	КонецЦикла
	
	УстановитьЭлементСтроки(ТекСтрока,Объект);
	АктивизироватьПоле();	
КонецПроцедуры

//___________________________________________________________________________________________
Процедура АктивизироватьПоле()
	ТекЗначение=Таблица.Значение(Таблица.ТекущаяСтрока(),Таблица.ТекущаяКолонка());
	Если ТипЗначения(ТекЗначение)>5 И ПустоеЗначение(ТекЗначение)=1 Тогда
		Если ТекЗначение.Выбрать() Тогда
			Таблица.Значение(Таблица.ТекущаяСтрока(),Таблица.ТекущаяКолонка(),ТекЗначение);
			ОбработкаСобытия("Окончание редактирования",0,0,0,0,0,0,0);
		КонецЕсли
		Возврат;
	КонецЕсли
	Таблица.Активизировать(Таблица.ТекущаяСтрока(),Таблица.ТекущаяКолонка());
КонецПроцедуры

//___________________________________________________________________________________________
Процедура НачалоРедактирования();
	Если ВиртуальныйРежим Тогда
		ТекЗначение=ПолучитьЭлементСтроки(Таблица.ТекущаяСтрока());
		ОткрытьФорму(ТекЗначение);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли
	
	НомерРедактируемойСтроки=Таблица.ТекущаяСтрока();
	Таблица.Редактирование(1);
	
	Если НомерНовойСтроки=0 И СостояниеКлавиши("F2")=0 Тогда
		ТекЗначение=Таблица.Значение(Таблица.ТекущаяСтрока(),Таблица.ТекущаяКолонка());
		Если ТипЗначения(ТекЗначение)>5 Тогда
			ТекЗначение=ЗначениеИзСтроки(ЗначениеВСтроку(ТекЗначение));
			Если ТекЗначение.Выбрать() Тогда
				Таблица.Значение(Таблица.ТекущаяСтрока(),Таблица.ТекущаяКолонка(),ТекЗначение);
				Таблица.Обновить();
				Записать();
			КонецЕсли
			СтатусВозврата(0);
			Возврат;
		КонецЕсли
	КонецЕсли
КонецПроцедуры
//___________________________________________________________________________________________
Процедура Редактирование()Экспорт
	Если ВиртуальныйРежим Тогда
		ТекЗначение=ПолучитьЭлементСтроки(Таблица.ТекущаяСтрока());
		ОткрытьФорму(ТекЗначение);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли
	
	НомерРедактируемойСтроки=Таблица.ТекущаяСтрока();
	НомерНовойСтроки=Таблица.ТекущаяСтрока();
	АктивизироватьПоле();
КонецПроцедуры


//___________________________________________________________________________________________
Функция Выбрать()Экспорт
	
	//	Если ВРЕГ(РежимОткрытия)="ВЫБОР" И СостояниеКлавиши("SHIFT")=0 И НомерРедактируемойСтроки=0 Тогда
	Если КонтекстФормы.Форма.РежимВыбора() И СостояниеКлавиши("SHIFT")=0 И НомерРедактируемойСтроки=0 Тогда
		КонтекстФормы.Форма.ВыполнитьВыбор(ТекущийЭлемент());
		Таблица.Редактирование(0);
		
		Возврат 1;
	КонецЕсли
	
	Если ВиртуальныйРежим Тогда
		ТекЗначение=ПолучитьЭлементСтроки(Таблица.ТекущаяСтрока());
		ОткрытьФорму(ТекЗначение);
		Возврат 1;
	КонецЕсли
	
	Возврат 0;
КонецФункции


//___________________________________________________________________________________________
Процедура ИнициализацияОбъекта(Конт)Экспорт
	КонтекстФормы=Конт;
КонецПроцедуры//ИнициализацияОбъекта

////___________________________________________________________________________________________
//Процедура Деструктор()
//	Сообщить("Удален !!");
//КонецПроцедуры//Деструктор
